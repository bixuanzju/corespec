%% This file uses the Ott tool to specify the grammar, typing and
%% evaluation rules for the implicit and explicit languages

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% METAVARIABLES

metavar tmvar, x, y, f, m, n ::=
  {{ repr-locally-nameless }}
  {{ com variables }}
  {{ lex alphanum }}

metavar covar, c ::=
  {{ repr-locally-nameless }}
  {{ com coercion variables }}
  {{ lex alphanum }}

metavar datacon, K ::= {{ coq atom }}

metavar const, T ::= {{ coq atom }}
  {{ lex Alphanum }}

metavar tyfam, F ::= {{ coq atom }}
  {{ lex Alphanum }}

indexvar index, i ::=
  {{ tex \mathit{[[index]]} }} {{ com indices }} {{ coq nat }}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% GRAMMAR

grammar

role, R :: '' ::= {{ com Role }}
   | Nom  ::    :: Nom
   | Rep  ::    :: Rep
   | min R1 R2  :: S :: Min
     {{ coq (min [[R1]][[R2]]) }}
     {{ tex [[R1]] \cap [[R2]] }}


relflag, rho {{ tex \rho }} :: '' ::= {{ com relevance flag }}
   | +             ::   :: Rel
   | -             ::   :: Irrel

constraint, phi {{ tex \phi }} :: '' ::= {{ com props }}
   | a ~ b : A / R   ::   :: Eq
     {{ tex [[a]] [[~]]_{[[A]]/[[R]]} [[b]] }}
   | ( phi )          :: S :: Paren
     {{ coq ([[phi]]) }}
   | phi { b / x }    :: S :: Subst
     {{ coq (open_constraint_wrt_tm [[x b]] [[phi]]) }}
   | | phi |          :: S :: Erase
     {{ coq (erase_constraint [[phi]]) }}


tm, a, b, v, w, A, B :: 'a_' ::= {{ com types and kinds }}
   | TYPE               ::   :: Star
     {{ tex \star }}
   | x                  ::   :: Var
   | \ rho x : A / R . b    ::   :: Abs (+ bind x in b +)
     {{ tex \mathrm{\lambda}^[[rho]][[x]]\!:\![[A]]/[[R]].[[b]] }}
   | \ rho x . b        ::   :: UAbs (+ bind x in b +)
     {{ tex \mathrm{\lambda}^[[rho]][[x]].[[b]] }}
   | a rho b            ::   :: App
     {{ tex [[a]]\ [[b]]^{[[rho]]}  }}
   | F                  ::   :: Fam
   | T                  ::   :: Const
   | all rho x : A / R -> B ::   :: Pi (+ bind x in B +)
     {{ tex \mathrm{\Pi}^[[rho]][[x]]\!:\![[A]]/[[R]][[->]][[B]] }}
   | a |> g             ::   :: Conv
   | all c : phi . B    ::   :: CPi (+ bind c in B +)
     {{ tex [[all]][[c]]\!:\![[phi]].[[B]] }}
   | /\ c : phi . b     ::   :: CAbs (+ bind c in b +)
     {{ tex [[/\]][[c]]\!:\![[phi]].[[b]] }}
   | /\ c  . b          ::   :: UCAbs (+ bind c in b +)
     {{ tex [[/\]][[c]].[[b]] }}
   | a [ g ]            ::   :: CApp
   | []                 ::   :: Bullet

   %% currently unused but ready for extension
   | K                ::   :: DataCon
   | match a with brs ::   :: Case


   %% standard substitution, using open
   | a { b / x }      :: S :: Subst
     {{ coq (open_tm_wrt_tm [[x a]] [[b]]) }}

   %% this is a hack for when a substitution {x/x} is necessary for the
   %% locally nameless backend to generate the correct code, but we don't
   %% want this substitution to show in the tex output

   | a {{{ b / x }}}  :: S :: SecretSubst
     {{ coq (open_tm_wrt_tm [[x a]] [[b]]) }}
     {{ tex [[a]] }}

   | a { g / c }      :: S :: SubstC
     {{ coq (open_tm_wrt_co [[c a]] [[g]])  }}
   | a {{{ g / c }}}  :: S :: SecretSubstC
     {{ coq (open_tm_wrt_co [[c a]] [[g]])  }}
     {{ tex [[a]] }}


   | ( a )            :: S :: Paren
     {{ coq ([[a]]) }}
   %% Ott parsing precedence is hard
   | (# a #)          :: S :: SecretParen
     {{ coq [[a]] }} {{ tex [[a]] }}
     {{ com parsing precedence is hard }}

   |  | a |           :: S :: Erase   {{ coq (erase_tm [[a]]) }}

   %% abbreviations for examples
   | Int              :: S :: Int    {{ coq 0 }}
   | Bool             :: S :: Bool   {{ coq 0 }}
   | Nat              :: S :: Nat    {{ coq 0 }}
   | Vec              :: S :: Vec    {{ coq 0 }}
   | 0                :: S :: Zero   {{ coq 0 }}
   | Succ             :: S :: Succ   {{ coq 0 }} {{ tex \mathsf{S} }}
   | True             :: S :: True   {{ coq 0 }}
   | Fix              :: S :: Fix    {{ coq 0 }}
   | a -> b           :: S :: Arrow  {{ coq (a_Pi [[a]] [[b]]) }}
   | phi => A         :: S :: CArrow {{ coq (a_CPi [[phi]] [[A]]) }}
   | a b              :: S :: RelApp
     {{ coq (a_App [[a]] Rel [[b]]) }}
   | \ x . a          :: S :: RelUAbs
     {{ coq (a_UAbs Rel [[a]]) }}
     {{ tex \lambda [[x]].[[a]] }}
   | \ x : A / R . a      :: S :: RelAbs
     {{ coq (a_Abs Rel [[A]] [[R]] [[a]]) }}
     {{ tex \lambda [[x]]\!:\![[A]].[[a]] }}
   | all x : A / R -> B   :: S :: RelPi
     {{ coq (a_Pi  Rel [[A]] [[R]] [[B]]) }}

%% Currently unused
brs :: 'br_' ::=    {{ com case branches }}
   | none             ::   :: None
   | K => a ; brs     ::   :: One
   | brs { a / x }    :: S :: Subst  {{ coq (open_brs_wrt_tm [[x brs]] [[a]]) }}
   | brs { g / c }    :: S :: SubstC {{ coq (open_brs_wrt_co [[c brs]] [[g]]) }}
   | ( brs )          :: S :: Paren  {{ coq ([[brs]]) }}

co, g {{ tex \gamma }} :: 'g_' ::= {{ com explicit coercions }}
   | o                    ::   :: Triv
   | c                    ::   :: Var
   | red a b              ::   :: Beta

   | refl a               ::   :: Refl
   | refl2 a b g          ::   :: Refl2
     {{ tex ([[a]]\mathrel{|\!\!=\!\!|}_{[[g]]}[[b]]) }}
   | sym g                ::   :: Sym
   | g1 ; g2              ::   :: Trans

   | all rho x : g1 -> g2 ::   :: PiCong (+ bind x in g2 +)
     {{ tex \mathrm{\Pi}^[[rho]][[x]]\!:\![[g1]].[[g2]] }}

   | \ rho x : g1 . g2    ::   :: AbsCong (+ bind x in g2 +)
     {{ tex \mathrm{\lambda}^[[rho]][[x]]\!:\![[g1]].[[g2]] }}

   | g1 rho g2            ::   :: AppCong
     {{ tex [[g1]]\ [[g2]]^[[rho]] }}

   | piFst g              ::   :: PiFst
   | cpiFst g             ::   :: CPiFst
   | isoSnd g            ::   :: IsoSnd
   | g1 @ g2              ::   :: PiSnd

   | all c : g1 . g3      ::   :: CPiCong (+ bind c in g3 +)
     {{ tex [[all]][[c]]\!:\![[g1]].[[g3]] }}

   | \ c : g1 . g3 @ g4   ::   :: CAbsCong (+ bind c in g3 +)
     {{ tex \mathrm{\lambda}[[c]]\!:\![[g1]].[[g3]][[@]][[g4]] }}

   | g ( g1 , g2 )        ::   :: CAppCong
   | g @ ( g1 , g2 )      ::   :: CPiSnd
     {{ tex [[g]] [[@]] ([[g1]] \sim [[g2]]) }}
   | g1 |> g2             ::   :: Cast
   | g1 ~ A / R  g2       ::   :: EqCong
     {{ tex [[g1]] [[~]]_{[[A]]/[[R]]} [[g2]] }}
   | conv phi1 phi2 g     ::   :: IsoConv
     {{ tex [[conv]]\ [[phi1]]\sim_[[g]] [[phi2]] }}

   | eta a                ::   :: Eta
   | left g g'            ::   :: Left
   | right g g'           ::   :: Right

   | ( g )                :: S :: Paren   {{ coq ([[g]]) }}
   | (# g #)              :: S :: SecretParen
     {{ coq [[g]] }} {{ tex [[g]] }}
   | g { a / x }          :: S :: TSubst
     {{ coq open_co_wrt_tm [[x g]] [[a]] }}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

substitutions
  single a x   :: tm_subst_tm
  single g x   :: co_subst_tm
  single a c   :: tm_subst_co
  single g c   :: co_subst_co
  single brs x :: brs_subst_tm
  single brs c :: brs_subst_co

freevars
  a x   :: fv_tm_tm
  g x   :: fv_co_tm
  a c   :: fv_tm_co
  g c   :: fv_co_co
  brs x :: fv_tm_brs
  brs c :: fv_co_brs

parsing
  a_Abs right a_App
  a_UAbs right a_App
  a_Abs <= a_RelApp
  a_UAbs <= a_RelApp
  a_App left a_App
  a_RelApp left a_App
  a_App left a_RelApp
  a_RelApp left a_RelApp
  a_CApp left a_App
  a_App left a_CApp
  a_CApp left a_RelApp
  a_RelApp left a_CApp

  a_Arrow right a_Arrow
  a_App right a_Arrow
  a_RelApp right a_Arrow
  a_Pi right a_Arrow
  a_CPi right a_Arrow

  a_Arrow <= a_Pi

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  This part is not handled by LNgen

grammar

sig_sort :: '' ::= {{ com signature classifier }}
   | Cs A            ::    :: Cs
   | Ax a A R        ::    :: Ax

sort :: '' ::= {{ com binding classifier }}
   | Tm A R           ::   :: Tm
   | Co phi           ::   :: Co


grammar

context, G {{ tex \Gamma }} :: 'ctx_' ::= {{ com contexts }}
                                          {{ coq list ( atom * sort ) }}
   | empty           ::    :: Empty   {{ coq nil }}

   | G , x : A / R   ::    :: ConsTm  {{ coq (([[x]]~ Tm [[A]][[R]]) ++ [[G]]) }}
   | G , c : phi     ::    :: ConsCo  {{ coq (([[c]]~ Co [[phi]]) ++ [[G]]) }}
   | G { b / x }     :: M  :: SubstA  {{ coq (open_context_wrt_tm [[x G]][[b]] }}
   | G { g / c }     :: M  :: SubstG  {{ coq (open_context_wrt_co [[c G]][[g]]  }}
   | G ++ G'         :: M  :: Append  {{ coq ([[G]] ++ [[G']]) }}
     {{ tex [[G]], [[G']] }}
   | | G |           :: M  :: Erase   {{ coq (erase_context [[G]]) }}
   | ( G )           :: M  :: Paren   {{ coq ([[G]]) }}
   | (# G #)         :: M  :: SecretParen {{ coq [[G]] }} {{ tex [[G]] }}


available_props, D {{ tex \Delta }} :: 'D_' ::= {{ coq atoms }} {{ coq-universe Type }}
   | emptyD          ::    :: Empty   {{ coq AtomSetImpl.empty }} {{ tex \varnothing }}
   | D , c           ::    :: ConsCo  {{ coq (singleton [[c]] \u [[D]]) }}
   | dom G           :: M  :: DomG    {{ coq (dom [[G]]) }} {{ tex \widetilde {[[G]]} }}
   | ( D )           :: M  :: Paren   {{ coq ([[D]]) }}

sig, S {{ tex \Sigma }} :: 'sig_' ::= {{ com signatures }} {{ coq list (atom * sig_sort) }}
   | emptyS         ::    :: Empty       {{ coq nil }}
     {{ tex \varnothing }}
   | S , T : A / R    ::    :: ConsCs      {{ coq (([[T]]~ Cs[[A]][[R]])++[[S]]) }}
     {{ tex [[S]] \cup \{[[T]] : [[A]]/[[R]]\} }}
   | S , F ~ a : A / R  ::    :: ConsAx      {{ coq (([[F]]~ Ax[[a]][[A]][[R]])++[[S]]) }}
     {{ tex [[S]] \cup \{[[F]]\sim[[a]] : [[A]]/[[R]]\} }}
   | toplevel       :: M  :: Toplevel
     {{ coq toplevel }}
     {{ tex \Sigma_0 }}
   | an_toplevel    :: M  :: AnToplevel
     {{ coq an_toplevel }}
     {{ tex \Sigma_1 }}
   | | S |          :: M  :: Erase  {{ coq (erase_sig [[S]]) }}



substitutions
  single G x :: ctx_subst_tm
  single G c :: ctx_subst_co

freevars
  G x :: fv_ctx_tm
  G c :: fv_ctx_co




%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Terminals  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

grammar

terminals :: 'terminals_' ::=
  | <->               ::   :: iso           {{ tex \leftrightarrow }}
  | <=>               ::   :: lrarrow       {{ tex \Leftrightarrow }}
  | -->               ::   :: step          {{ tex \longrightarrow }}
  | min               ::   :: min           {{ tex \mathsf{min} }}
  | ==                ::   :: definitional  {{ tex \equiv }}
  | all               ::   :: forall     {{ tex \forall }}
  | in                ::   :: in         {{ tex \in }}
  | notin             ::   :: notin      {{ tex \not\in }}
  | <=                ::   :: check      {{ tex \Leftarrow }}
  | =>                ::   :: infer      {{ tex \Rightarrow }}
  | =>*               ::   :: minfer      {{ tex \Rightarrow^{\ast} }}
  | ->                ::   :: arrow      {{ tex \to }}
  | /\                ::   :: ilam       {{ tex \mathrm{\Lambda} }}   % Swag
  | <-|               ::   :: mapsfrom   {{ tex \mapsfrom }}
  | []                ::   :: box        {{ tex \Box }}
  | |-                ::   :: entails    {{ tex \vdash }}
  | -|                ::   :: produces   {{ tex \dashv }}
  | |=                ::   :: hs_entails {{ tex \vDash }}
  | |==               ::   :: hs_entaill {{ tex \vDash }}
  | /=                ::   :: neq        {{ tex \neq }}
  | |>                ::   :: cast       {{ tex \triangleright }}
  | ok                ::   :: ok         {{ tex \ \mathsf{ok} }}
  | _                 ::   :: blank      {{ tex \_ }}
  | ~>                ::   :: produce    {{ tex \leadsto }}
  | ~>*               ::   :: multistep  {{ tex \leadsto^{\ast} }}
  | ~>h               ::   :: steph      {{ tex \leadsto }}
  | empty             ::   :: empty      {{ tex \varnothing }}
  | hole              ::   :: hole       {{ tex \circ }}
  | fv                ::   :: fv         {{ tex \mathsf{fv}\! }}
  | dom               ::   :: dom        {{ tex \mathsf{dom} }}
  | ~                 ::   :: twiddle    {{ tex \sim }}
  | ~~                ::   :: compatible {{ tex \asymp }}
  | '|'               ::   :: bar        {{ tex | }}
  | o                 ::   :: bullet     {{ tex \bullet }}
  | fst               ::   :: fst
  | snd               ::   :: snd
  | |=>|              ::   :: erasesto   {{ tex \mathrel{|\Rightarrow|} }}
  | |-=               ::   :: turneq     {{ tex \vdash_{=} }}
  | refl2             ::   :: refl2      {{ tex \mathbf{refl_2}  }}
  | ++                ::   :: concat     {{ tex \mathop{++} }}

formula, Fm {{tex \psi}} :: 'formula_' ::=
  | judgement                ::   :: judgement
  | x : A / R in G               ::   :: inG
    {{ coq binds [[x]] (Tm [[A]][[R]]) [[G]] }}

  | c : phi  in G         ::   :: inGP
    {{ coq binds [[c]] (Co [[phi]]) [[G]] }}

  | T : A / R in S             ::   :: inGF
    {{ coq binds [[T]] (Cs [[A]] [[R]]) [[S]] }}

  | F ~ a : A / R in S             ::   :: inGFam
    {{ coq binds [[F]] (Ax [[a]][[A]][[R]]) [[S]] }}

  | K : T G in S               ::   :: inSD
    {{ coq binds [[K]] ([[T]],[[G]]) [[S]] }}

  | x in D                   ::   :: availx
    {{ coq AtomSetImpl.In [[x]] [[D]] }}
  | c in D                   ::   :: avail
    {{ coq AtomSetImpl.In [[c]] [[D]] }}
  | c not relevant in g      ::   :: notrelevant
    {{ coq [[c]] \notin (fv_relevant [[g]]) }}
  | x notin fv a             ::   :: fresh
    {{ coq [[x]] \notin fv_tm_tm_tm [[a]] }}
  | x notin dom G            ::   :: notInGx
    {{ coq ~ AtomSetImpl.In [[x]] (dom [[G]]) }}
  | c notin dom G            ::   :: notInGc
    {{ coq ~ AtomSetImpl.In [[c]] (dom [[G]]) }}
  | T notin dom S            ::   :: notInST
    {{ coq ~ AtomSetImpl.In [[T]] (dom [[S]]) }}
  | F notin dom S            ::   :: notInSF
    {{ coq ~ AtomSetImpl.In [[F]] (dom [[S]]) }}
  | a = b                    ::   :: equal
    {{ coq ([[a]] = [[b]]) }}
  |  phi1 =  phi2       ::   :: erasedphi
    {{ coq ([[phi1]] = [[phi2]]) }}
  | G1 = G2                  ::   :: equalctx
    {{ coq ([[G1]] = [[G2]]) }}
  | g1 = g2                  ::   :: equalco
    {{ coq ([[g1]] = [[g2]]) }}
  | not Fm              ::   :: not
    {{ coq not ([[Fm]]) }}
    {{ tex \neg [[Fm]] }}
  | Fm1 /\ Fm2     ::   :: and
    {{ coq [[Fm1]] /\ [[Fm2]] }}
    {{ tex [[Fm1]] \wedge [[Fm2]] }}
  | Fm1 \/ Fm2     ::   :: or
    {{ coq [[Fm1]] \/ [[Fm2]] }}
    {{ tex [[Fm1]] \vee [[Fm2]] }}
  | Fm1 implies Fm2     ::   :: implies
    {{ coq [[Fm1]] -> [[Fm2]] }}
    {{ tex [[Fm1]] \Rightarrow [[Fm2]] }}
  | ( Fm )              ::   :: Paren
    {{ coq ([[Fm]]) }}
  | [ Fm ]              ::   :: Regular
    {{ coq ([[Fm]]) }}
    {{ tex \suppress{[[Fm]]} }}
  | c : (a : A ~ b : B) in G ::   :: Alt
    {{ coq True }}
  | nolc A                   ::   :: NoLC
    {{ com suppress lc hypothesis generated by Ott }}
    {{ coq True }}
	 {{ tex      }}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Coq Definitions necessary to specify the semantics

embed {{ coq

Definition min (r1 : role) (r2 : role) : role :=
  match r1 , r2 with
  | Nom, _ => Nom
  | _  , Nom => Nom
  | Rep, Rep => Rep
  end.

Fixpoint erase_tm (a : tm) : tm :=
   match a with
   | a_Star    => a_Star
   | a_Var_b n => a_Var_b n
   | a_Var_f x => a_Var_f x
   | a_Abs rho A R b => a_UAbs rho (erase_tm b)
   | a_UAbs rho b => a_UAbs rho (erase_tm b)
   | a_App a Rel b => a_App (erase_tm a) Rel (erase_tm b)
   | a_App a Irrel b => a_App (erase_tm a) Irrel a_Bullet
   | a_Const T => a_Const T
   | a_Fam F => a_Fam F
   | a_Pi rho A R B => a_Pi rho (erase_tm A) R (erase_tm B)
   | a_Conv a _ => erase_tm a
   | a_CPi phi B => a_CPi (erase_constraint phi) (erase_tm B)
   | a_CAbs phi b => a_UCAbs (erase_tm b)
   | a_UCAbs b => a_UCAbs (erase_tm b)
   | a_CApp a g => a_CApp (erase_tm a) g_Triv
   | a_DataCon K => a_Star  (* a_DataCon K *)
   | a_Case a brs => a_Star (* a_Case (erase_tm a) (erase_brs brs) *)
   | a_Bullet => a_Bullet
   end
with erase_brs (x : brs) : brs :=
   match x with
   | br_None => br_None
   | br_One k a y => br_One k (erase_tm a) (erase_brs y)
   end
with erase_constraint (phi : constraint) : constraint :=
   match phi with
   | Eq A B A1 R => Eq (erase_tm A) (erase_tm B) (erase_tm A1) R
   end.

Definition erase_sort s :=
 match s with
 | Tm a R => Tm (erase_tm a) R
 | Co p => Co (erase_constraint p)
end.


Definition erase_csort s :=
 match s with
 | Cs a   => Cs (erase_tm a)
 | Ax a A R => Ax (erase_tm a) (erase_tm A) R
end.

Definition erase_context G := map erase_sort G.
Definition erase_sig S := map erase_csort S.

(* -------------- A specific signature with Fix ------------ *)
Definition Fix : atom.
  pick fresh F.
  exact F.
Qed.

Definition FixDef : tm :=
  (a_Abs Irrel a_Star Nom
         (a_Abs Rel (a_Pi Rel (a_Var_b 0) Nom (a_Var_b 1)) Nom
                (a_App (a_Var_b 0) Rel
                       (a_App (a_App (a_Fam Fix) Irrel (a_Var_b 1)) Rel (a_Var_b 0))))).

Definition FixTy : tm :=
  a_Pi Irrel a_Star Nom
       (a_Pi Rel (a_Pi Rel (a_Var_b 0) Nom (a_Var_b 1)) Nom
             (a_Var_b 1)).


Definition an_toplevel : sig := Fix ~ Ax FixDef FixTy Nom.

Definition toplevel : sig := erase_sig an_toplevel.

}}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% "Syntactic" definitions

defns
JSubRole :: '' ::=

defn
R1 <= R2  ::  :: SubRole :: ''
{{ com Subroling judgement }}
{{ tex [[R1]] \leq [[R2]] }}
by

------------ :: NomRep
Nom <= Rep


------------ :: Refl
R <= R

R1 <= R2
R2 <= R3
--------- :: Trans
R1 <= R3

defns
JValue :: '' ::=

defn
CoercedValue A ::   :: CoercedValue :: ''
{{ com Values with at most one coercion at the top }}
by

Value a
----------------- :: CV
CoercedValue a

Value a
---------------------- :: CC
CoercedValue (a |> g)

defn
Value A  ::   :: Value    :: 'Value_'
{{ com values }}
{{ tex \suppress{\mathsf{Value}\ [[A]]} }}
by

------- :: Star
Value TYPE

-------------------------- :: Pi
Value all rho x : A / R -> B

-------------------- :: CPi
Value all c : phi. B

--------------------- :: AbsRel
Value \ + x:A/R. a

--------------------- :: UAbsRel
Value \ + x. a

Value a
--------------------- :: UAbsIrrel
Value \ - x. a

CoercedValue a
-------------------- :: AbsIrrel
Value \ - x : A/R . a

--------------------- :: CAbs
Value /\c:phi.a

--------------------- :: UCAbs
Value /\c .a


defn
ValueType A ::   :: value_type :: 'value_type_'
{{ com Types with head forms (erased language) }}
by

------- :: Star
ValueType TYPE

-------------------------- :: Pi
ValueType all rho x : A / R -> B

-------------------- :: CPi
ValueType all c : phi. B


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Definitions for consistency proof

defns
   Jconsistent :: '' ::=

defn
   consistent a b ::  :: consistent :: 'consistent_a_'
   {{ com (erased) types do not differ in their heads }}
by

---------------------- :: Star
consistent TYPE TYPE

------------------------ :: Pi
consistent (all rho x1:A1 / R -> B1) (all rho x2:A2 / R -> B2)

------------------------ :: CPi
consistent (all c1:phi1. A1) (all c2 :phi2. A2)

not ValueType b
---------------------- :: Step_R
consistent a b

not ValueType a
---------------------- :: Step_L
consistent a b

defns
  Jerased :: '' ::=

defn
  erased_tm a ::  :: erased_tm :: 'erased_a_'
by

--------- :: Bullet
erased_tm []

------------  :: Star
erased_tm TYPE

-------- :: Var
erased_tm x

erased_tm a
-------------------- :: Abs
erased_tm (\rho x.a)

%% Note, this is not quite right, but it is ok.
erased_tm a
erased_tm b
----------------- :: App
erased_tm (a rho b)

erased_tm A
erased_tm B
------------------------- :: Pi
erased_tm (all rho x:A / R -> B)

erased_tm a
erased_tm b
erased_tm A
erased_tm B
---------------------------------- :: CPi
erased_tm (all c:a ~ b : A / R. B)

erased_tm b
--------------- :: CAbs
erased_tm (/\c. b)

erased_tm a
--------------- :: CApp
erased_tm (a [o])

-------- :: Fam
erased_tm F

------------ :: Const
erased_tm T

defns
JChk :: '' ::=

defn
rho => check x in A ::    :: RhoCheck :: 'Rho_'
{{ tex ([[rho]] = +) \vee ([[x]]\not\in\mathsf{fv}\; [[A]]) }}
{{ com irrelevant argument check }}
by

nolc A
-------------------- :: Rel
+ => check x in A

x notin fv A
-------------------- :: IrrRel
- => check x in A


defns
  Jpar :: '' ::=


defn
G ; D |- a => b ::  :: Par :: 'Par_'
{{ tex \vDash [[a]] [[=>]] [[b]] }}
{{ com parallel reduction (implicit language) }}
by

----------------------- :: Refl
G ; D |- a => a


G ; D |- a => (\rho x.a')
G ; D |- b => b'
---------------------------- :: Beta
G ; D |- a rho b => a' { b' / x }

G ; D |- a => a'
G ; D |- b => b'
-------------------------- :: App
G ; D |- a rho b => a' rho b'

G ; D |- a => (/\c .a')
----------------------- :: CBeta
G ; D |- a [o] => a' { o / c }

G ; D |- a => a'
----------------------- :: CApp
G ; D |- a [o] => a' [o]

G ; D |- a => a'
-------------------------------- :: Abs
G ; D |- \ rho x. a => \ rho x. a'

G ; D |- A => A'
G ; D |- B => B'
-------------------------------------------- :: Pi
G ; D |- all rho x:A/R -> B => all rho x:A'/R -> B'

G ; D |- a => a'
--------------------------- :: CAbs
G ; D |- /\c . a => /\c . a'

G ; D |- A => A'
G ; D |- B => B'
G ; D |- a => a'
G ; D |- A1 => A1'
----------------------------------------- :: CPi
G ; D |- all c:A~B:A1/R. a => all c:A'~B':A1'/R. a'

% NOTE: toplevel is assumed to be locally closed
F ~ a : A/R in toplevel
-------------------------------- :: Axiom
G ; D |- F => a


defn
G ; D |- a =>* b ::   :: MultiPar :: 'MP_'
{{ tex [[|-]] [[a]] [[=>*]] [[b]] }}
{{ com multistep parallel reduction }}
by

------------------ :: Refl
G ; D |- a =>* a

G ; D |- a => b
G ; D |- b =>* a'
--------------------- :: Step
G ; D |- a =>* a'

defn
G ; D |- a <=> b ::   :: joins :: ''
{{ tex [[|-]] [[a]] [[<=>]] [[b]] }}
{{ com parallel reduction to a common term }}
by

G ; D |- a1 =>* b
G ; D |- a2 =>* b
------------------------ :: join
G ; D |- a1 <=> a2


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Implicit (or erased) language reductions

defns
 Jbeta :: '' ::=

defn
|= a > b  ::     :: Beta :: 'Beta_'
{{ com primitive reductions on erased terms }}
by

Value (\rho x. v)
------------------------------------ :: AppAbs
|= (\rho x.v) rho b > v { b / x }


------------------------------- :: CAppCAbs
|= (/\c .a') [o] > a' { o / c }


F ~ a : A/R in toplevel
-------------------------------- :: Axiom
|= F > a


defn
 |= a ~> b ::   :: reduction_in_one :: 'E_'
{{ com single-step head reduction for implicit language }}
by

|= a ~> a'
-------------------------  :: AbsTerm
|= \- x.a ~> \- x.a'

|= a ~> a'
-------------------------  :: AppLeft
|= a rho b ~> a' rho b

|= a  ~> a'
-----------------------------   :: CAppLeft
|= a [o]  ~> a' [o]


Value (\rho x. v)
----------------------------------  :: AppAbs
|= (\rho x.v) rho a ~> v { a / x}

--------------------------------  :: CAppCAbs
|= (/\c .b) [o] ~> b { o / c}

F ~ a : A/R in toplevel
-------------------- :: Axiom
|= F ~> a


defn

|= a ~>* b ::  :: reduction :: ''
{{ com multistep reduction }}
by

------------------- :: Equal
|= a ~>* a

|= a ~> b
|= b ~>* a'
------------------- :: Step
|= a ~>* a'

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Implicit (or erased) language type system

defns
Jett :: '' ::=


defn
G |= phi ok ::    :: PropWff :: 'E_'
{{ com Prop wellformedness }}
by

G |= a : A / R
G |= b : A / R
[ G |= A : TYPE / R ]
---------------------------  :: Wff
G |= a ~ b : A / R  ok

defn

G |= a : A / R ::   :: Typing :: 'E_'
{{ com typing }}
by

R1 <= R2
G |= a : A / R1
----------------- :: SubRole
G |= a : A / R2

|= G
---------------------  :: Star
G |= TYPE : TYPE / R

|= G
x:A / R in G
------------------ :: Var
G |= x : A / R

%% 2nd premise is redundant, but convenient for IH
G, x:A / R |= B : TYPE / R
[ G |= A : TYPE / R ]
----------------------------------------- :: Pi
G |= all rho x:A / R -> B : TYPE / R

G, x:A / R  |= a : B / R'
[ G |= A : TYPE / R ]
rho => check x in a
R <= R'
---------------------------------- :: Abs
G |= \ rho x . a : ( all rho x:A / R -> B ) / R'

G |= b : all+ x : A / R -> B / R'
G |= a : A / R
------------------------------------- :: App
G |= b + a : B { a / x } / R'

G |= b : all - x : A / R -> B / R'
G |= a : A / R
------------------------------------- :: IApp
G |= b - [] : B { a / x } / R'


G |= a : A / R
G ; dom G |= A == B : TYPE / R
[ G |= B : TYPE / R ]
----------------------------- :: Conv
G |= a : B / R

G, c : phi |= B : TYPE / R
[ G |= phi ok ]
-------------------------------- :: CPi
G |= all c:phi. B : TYPE / R

G, c:phi |= a : B / R
[ G |= phi ok ]
------------------------------------------------- :: CAbs
G |= /\ c . a : all c:phi. B / R

G |= a1 : all c : (a ~ b : A / R) . B1 / R'
G ; dom G |= a == b : A / R
------------------------------------------- :: CApp
G |= a1 [ o ] : B1 { o / c } / R'

|= G
F ~ a : A / R in toplevel
[ empty |= A : TYPE / R ]
------------------ :: Fam
G |= F : A / R


defn
G ; D |= phi1 == phi2  ::    :: Iso :: 'E_'
{{ com prop equality }}
by

G ; D |= A1 == A2 : A / R
G ; D |= B1 == B2 : A / R
--------------------------------------------- :: PropCong
G ; D |= A1 ~ B1 : A / R == A2 ~ B2 : A / R

G ; D |= A == B : TYPE / R
G |= A1 ~ A2 : A / R ok
G |= A1 ~ A2 : B / R ok
-------------------------------------------- :: IsoConv
G ; D |= A1 ~ A2 : A / R == A1 ~ A2 : B / R

G ; D |= all c:phi1. B1 == all c:phi2. B2 : TYPE / R
----------------------------------------------------- :: CPiFst
G ; D |=  phi1 == phi2


defn

G ; D |= a == b : A / R ::     :: DefEq :: 'E_'
{{ com definitional equality }}
by

|= G
c: (a ~ b : A / R ) in G
c in D
-------------------- :: Assn
G ; D |= a == b : A / R

G |= a : A / R
------------------------ :: Refl
G ; D |= a == a : A / R

G ; D |= b == a : A / R
------------------------ :: Sym
G ; D |= a == b : A / R

G ; D |= a == a1 : A / R
G ; D |= a1 == b : A / R
------------------------------------- :: Trans
G ; D |= a == b : A / R

G ; D |= a == b : A / R1
R1 <= R2
---------------------------------------- :: Sub
G ; D |= a == b : A / R2

G |= a1 : B / R
[G |= a2 : B / R ]
|= a1 > a2
--------------------------------  :: Beta
G ; D |= a1 == a2 : B / R

%% Rules related to functions

G ; D |=  A1 == A2 : TYPE/R
G, x:A1/R; D |= B1 == B2 : TYPE/R
%% this needed by weakening IH
[G |= A1 : TYPE/R]
%% these two needed by regularity
[G |= all rho x:A1/R -> B1 : TYPE/R]
[G |= all rho x:A2/R -> B2 : TYPE/R]
---------------------------------------------------------- :: PiCong
G ; D |= (all rho x:A1/R -> B1) == (all rho x:A2/R -> B2) : TYPE/R


G, x:A1/R ; D |= b1 == b2 : B/R'
[ G |= A1 : TYPE/R ]
[ R <= R' ]
rho => check x in b1
rho => check x in b2
-------------------------------------------------------------- :: AbsCong
G ; D |=  (\ rho x. b1) == (\ rho x. b2) : (all rho x:A1/R -> B)/R'

G ; D |= a1 == b1 : (all + x:A/R -> B)/R'
G ; D |= a2 == b2 : A/R
-------------------------------------- :: AppCong
G ; D |= a1 + a2 == b1 + b2 : (B { a2 / x })/R'


G ; D |= a1 == b1 : (all - x:A/R -> B)/R'
G |= a : A/R
------------------------------------------ :: IAppCong
G ; D |= a1 - [] == b1 - [] : (B { a / x })/R'


G ; D |= all rho x:A1/R1 -> B1 == all rho x:A2/R2 -> B2 : TYPE/R'
------------------------------------------------- :: PiFst
G ; D |=  A1 == A2 : TYPE/ min R1 R2

G ; D |= all rho x:A1/R1 -> B1 == all rho x:A2/R2 -> B2 : TYPE/R'
G ; D |= a1 == a2 : A1/ min R1 R2
------------------------------------------- :: PiSnd
G ; D |=  B1 {a1 /x}  == B2 {a2/x} : TYPE/R'

%% Rules related to Coercion Abstraction

G ; D |= phi1 == phi2
G, c: phi1 ; D |= A == B : TYPE/R
[G |= phi1 ok]
[G |= all c:phi1. A : TYPE/R]
[G |= all c:phi2. B : TYPE/R]
--------------------------------------------------------- :: CPiCong
G ; D |= all c:phi1. A  == all c:phi2. B : TYPE/R

G, c: phi1 ; D |= a == b : B/R
[ G |= phi1 ok ]
------------------------------------------------------------ :: CAbsCong
G ; D |=  (/\c . a) == (/\ c . b) : all c:phi1. B/R

G ; D |= a1 == b1 : (all c:(a ~ b : A/R). B)/R'
G ; dom G |= a == b : A/R
-------------------------------------- :: CAppCong
G ; D |= a1 [o] == b1 [o] : (B { o / c })/R'

G ; D |= all c:(a1 ~ a2 : A/R). B1 == all c:(a1'~ a2' : A'/R'). B2 : TYPE/R0
G ; dom G |= a1 == a2 : A/R
G ; dom G |= a1' == a2' : A'/R'
---------------------------------------------- :: CPiSnd
G ; D |=  B1 { o / c} == B2 { o / c } : TYPE/R0

%% Other rules

G ; D |= a == b : A/R
G ; D |= a ~ b : A/R == a' ~ b' : A'/R'
-------------------------------------- :: Cast
G ; D |= a' == b' : A'/R'

G ; D |= a == b : A/R1
G ; dom G |= A == B : TYPE/R2
[ R1 <= R2 ]
-------------------------------------- :: EqConv
G ; D |= a == b : B/R2

G ; D |= a ~ b : A/R == a' ~ b' : A'/R'
R <= R0
R' <= R0
-------------------------------------- :: IsoSnd
G ; D |= A == A' : TYPE/R0



defn
|= G ::    :: Ctx  :: 'E_'
{{ com context wellformedness }}
by

---------- :: Empty
|= empty

|= G
G |= A : TYPE / R
x notin dom G
--------------------- :: ConsTm
|= G, x : A / R

|= G
G |= phi ok
c notin dom G
-------------------- :: ConsCo
|= G, c: phi

defns

Jsig :: '' ::=

defn
|= S ::    :: Sig :: 'Sig_'
{{ com signature wellformedness }}
by

------------ :: Empty
|= emptyS

|= S
empty |= A : TYPE/R
empty |= a : A/R'
F notin dom S
[R' <= R]
---------------- :: ConsAx
|= S, F ~ a : A/R'

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Annotated language semantics
%

defns
Jann :: '' ::=

defn
G |- phi ok ::   :: AnnPropWff :: 'An_'
{{ com prop wellformedness }}
by

G |- a : A/R
G |- b : B/R
|A| = |B|
------------------------- :: Wff
G |- a ~ b : A/R  ok

defn
G |- a : A / R ::  :: AnnTyping :: 'An_'
{{ com typing }}
by

|- G
------------  :: Star
G |- TYPE : TYPE/R

|- G
x : A/R in G
----------- :: Var
G |- x : A/R

G, x:A/R |- B : TYPE/R'
[ G |- A : TYPE/R ]
------------------------------------ :: Pi
G |- all rho x:A/R -> B : TYPE/R'

[ G |- A : TYPE/R ]
G, x:A/R |- a : B/R'
rho => check x in | a |
R <= R'
---------------------------------- :: Abs
G |- \ rho x : A/R. a : (all rho x:A/R -> B)/R'

G |- b : (all rho x:A/R -> B)/R'
G |- a : A/R
------------------------------------- :: App
G |- b rho a : (B { a / x })/R'

G |- a : A/R
G ; dom G |- g : A ~ B : R
G |- B : TYPE/R
----------------------------- :: Conv
G |- a |> g : B/R

[ G |- phi ok ]
G, c : phi |- B : TYPE/R
-------------------------------- :: CPi
G |- all c:phi. B : TYPE/R

[ G |- phi ok ]
G, c:phi |- a : B/R
------------------------------------------------- :: CAbs
G |- /\ c: phi. a : (all c:phi. B)/R

G |- a1 : (all c : a ~ b : A1/R. B)/ R'
G ; dom G |- g : a ~ b : R
------------------------------------- :: CApp
G |- a1 [ g ] : B { g / c } / R'

|- G
F ~ a : A/R in an_toplevel
[ empty |- A : TYPE/R ]
----------------------- :: Fam
G |- F : A/R

defn
G ; D |- g : phi1 == phi2 ::  :: AnnIso :: 'An_'
{{ tex [[G]];[[D]][[|-]] [[g]] : [[phi1]] \sim [[phi2]] }}
{{ com coercion between props }}
by

G ; D |- g1 : A1 ~ A2 : R
G ; D |- g2 : B1 ~ B2 : R
G |- A1 ~ B1 : A/R ok
G |- A2 ~ B2 : A/R ok
------------------------------------------------------------ :: PropCong
G ; D |- (g1 ~A/R g2) : (A1 ~ B1 : A/R) == (A2 ~ B2 : A/R)

G ; D |- g : all c:phi1. A2 ~ all c:phi2. B2 : R
--------------------------------------- :: CPiFst
G ; D |- cpiFst g : phi1 == phi2

G ; D |- g : phi1 == phi2
---------------------------- :: IsoSym
G ; D |- sym g : phi2 == phi1

G; D |- g : A ~ B : R
G |- a1 ~ a2 : A/R ok
G |- a1' ~ a2' : B/R ok
|a1| = |a1'|
|a2| = |a2'|
-------------------------------------- :: IsoConv
G ; D |- conv (a1 ~ a2 : A/R) (a1' ~ a2' : B/R) g : (a1 ~ a2 : A/R) == (a1' ~ a2' : B/R)


defn
G ; D |- g : A ~ B : R ::  :: AnnDefEq :: 'An_'
{{ com coercion between types }}
{{ tex [[G]]; [[D]] \vdash [[g]] [[:]] [[A]] \sim_[[R]] [[B]] }}
by

|- G
c: a ~ b : A/R in G
c in D
------------------- :: Assn
G ; D |- c : a ~ b : R

G |- a : A/R
----------------------------- :: Refl
G ; D |- refl a : a ~ a : R

G |- a : A/R
G |- b : B/R
|a| = |b|
G ; dom G |- g : A ~ B : R
----------------------------- :: EraseEq
G ; D |- refl2 a b g : a ~ b : R


G |- b : B/R
G |- a : A/R
[ G ; dom G |- g1 : B ~ A : R ]
G ; D |- g : b ~ a : R
-------------------------- :: Sym
G ; D |- sym g : a ~ b : R

G ; D |- g1 : a ~ a1 : R
G ; D |- g2 : a1 ~ b : R
[ G |- a : A/R ]
[ G |- a1 : A1/R ]
[ G ; dom G |- g3 : A ~ A1 : R ]
------------------------------------- :: Trans
G ; D |- (g1 ; g2) : a ~ b : R

G |- a1 : B0/R
G |- a2 : B1/R
|B0| = |B1|
|= |a1| > |a2|
--------------------------------  :: Beta
G ; D |- red a1 a2 : a1 ~ a2 : R


%% Note: need to include {x/x} to emphasize that x is free in B2 for
%% the locally nameless backend.
%% Need all three assns, so that we know A1, A2, B1, B2, B3 all have kind TYPE
G ; D |- g1 : A1 ~ A2 : R
G, x : A1/R ; D |- g2 : B1 ~ (# B2 {{{ x / x }}} #) : R
B3 = B2 { x |> sym g1 / x }
G |- all rho x:A1/R -> B1 : TYPE/R
G |- all rho x:A1/R -> B2 : TYPE/R
G |- all rho x:A2/R -> B3 : TYPE/R
---------------------------------------------------------- :: PiCong
G ; D |- all rho x:g1 -> g2 : (all rho x:A1/R -> B1) ~ (all rho x:A2/R -> B3) : R

G ; D |- g1 : A1 ~ A2 : R
G, x : A1/R ; D |- g2 : b1 ~ (# b2 {{{x / x}}} #) : R'
b3 = b2 { x |> sym g1 / x }
[ G |- A1 : TYPE/R ]
G |- A2 : TYPE/R
rho => check x in | b1 |
rho => check x in | b3 |
[G |- (\ rho x:A1/R. b2) : B/R']
[R <= R' ]
------------------------------------------------------------------ :: AbsCong
G ; D |- (\rho x:g1. g2) : (\ rho x:A1/R. b1) ~ (\ rho x :A2/R. b3) : R'

% really want heterogenous equality here.
%% NOTE: we know that G |- g3 : A ~ B  must hold (b/c reg), but adding it as
%% an extra assumption for the erasure proof.
G ; D |- g1 : a1 ~ b1 : R'
G ; D |- g2 : a2 ~ b2 : R
G |- a1 rho a2 : A/R'
G |- b1 rho b2 : B/R'
[ G ; dom G |- g3 : A ~ B : R' ]
[ R <= R' ]
-------------------------------------- :: AppCong
G ; D |- g1 rho g2 : a1 rho a2 ~ b1 rho b2 : R'


G ; D |- g : all rho x:A1/R1 -> B1 ~ all rho x:A2/R2 -> B2 : R
R3 <= R1
R3 <= R2
--------------------------------------- :: PiFst
G ; D |- piFst g : A1 ~ A2 : R3

G ; D |- g1 : all rho x:A1/R1 -> B1  ~ all rho x:A2/R2 -> B2 : R'
G ; D |- g2 : a1 ~ a2 : R
G |- a1 : A1/R
G |- a2 : A2/R
R <= R1
R <= R2
--------------------------------------- :: PiSnd
G ; D |- g1 @ g2 : (# B1 {a1 /x} #) ~ (# B2 {a2/x} #) : R'


G ; D |- g1 : phi1 == phi2
G, c:phi1 ; D |- g3 : B1 ~ (# B2 {{{c/c}}} #) : R
B3 = B2 { c |> sym g1 / c }
G |- all c:phi1 . B1 : TYPE/R
[G |- all c:phi2 . B3 : TYPE/R]
G |- all c:phi1 . B2 : TYPE/R
----------------------------------------------------- :: CPiCong
G ; D |- (all c: g1. g3) : (all c:phi1. B1) ~ (all c:phi2. B3) : R


G ; D |- g1 : phi1 == phi2
G, c:phi1 ; D |- g3 : a1 ~ (# a2{{{c/c}}} #) : R
a3 = a2 { c |> sym g1 / c }
%% To know that the two types are equal, we need to know
%% that the range types can be shown identical without using
%% the assumption c:phi1. This is a stronger property than
%% regularity implies.
G |- (/\ c:phi1. a1) : all c:phi1. B1/R
G |- (/\ c:phi1. a2) : B/R
G |- (/\ c:phi2. a3) : all c:phi2. B2/R
G ; dom G |- g4 : all c:phi1. B1 ~ all c:phi2. B2 : R
---------------------------------------------------------------- :: CAbsCong
G ; D |- (\ c: g1. g3 @ g4) : (/\ c:phi1. a1) ~ (/\ c:phi2. a3) : R


G ; D |- g1 : a1 ~ b1 : R
G ; dom G |- g2 : a2 ~ b2 : R'
G ; dom G |- g3 : a3 ~ b3 : R'
G |- a1 [g2] : A / R
G |- b1 [g3] : B / R
[ G ; dom G |- g4 : A ~ B : R ]
-------------------------------------- :: CAppCong
G ; D |- g1 (g2, g3) : a1 [ g2 ]  ~ b1 [ g3 ] : R


G ; D |- g1 : (all c1:a~a':A/R. B1) ~ (all c2:b~b':B/R'. B2) : R0
G ; dom G |- g2 : a~a' : R
G ; dom G |- g3 : b~b' : R'
-------------------------------------------------- :: CPiSnd
G ; D |- g1 @ (g2, g3) : B1 { g2 / c1 } ~ B2 { g3 / c2 } : R0


G ; D |- g1 : a~a' : R1
G ; D |- g2 : a~a' :A/R1 == b~b' : B/R2
--------------------------------------- :: Cast
G ; D |- g1 |> g2 : b~b' : R2


G ; D |- g : (a~a' : A/R1) == (b~b' : B/R2)
R1 <= R0
R2 <= R0
---------------------------------- :: IsoSnd
G ; D |- isoSnd g : A ~ B : R0

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

defn
|- G ::      :: AnnCtx :: 'An_'
{{ com context wellformedness }}
by


---------- :: Empty
|- empty

|- G
G |- A : TYPE/R
x notin dom G
--------------------- :: ConsTm
|- G, x : A/R

|- G
G |- phi ok
c notin dom G
-------------------- :: ConsCo
|- G, c:phi

defn
|- S ::    :: AnnSig :: 'An_Sig_'
{{ com signature wellformedness }}
by

------------ :: Empty
|- emptyS


|- S
empty |- A : TYPE/R
empty |- a : A/R
F notin dom S
---------------- :: ConsAx
|- S, F ~ a  : A/R


defns
  Jred :: '' ::=

defn
G |- a ~>h b ::   :: head_reduction :: 'An_'
{{ com single-step, weak head reduction to values for annotated language }}
% NOTE: this reduction needs a context for the the
% two push rules. We need to read the coercion proofs
% to know what type annotations to use after the push.
by

G |- a ~>h a'
-------------------------  :: AppLeft
G |- a rho b ~>h a' rho b

Value (\rho x:A/R.w)
-----------------------------------------  :: AppAbs
G |- (\rho x:A/R.w) rho a ~>h w { a / x}


G |- a  ~>h a'
-----------------------------   :: CAppLeft
G |- a [g]  ~>h a' [g]


------------------------------------  :: CAppCAbs
G |- (/\c:phi.b) [g] ~>h b { g / c}

G |- A : TYPE/R
G, x : A/R |- b ~>h b'
------------------------------------------- :: AbsTerm
G |- (\- x:A/R. b) ~>h (\- x:A/R. b')


F ~ a : A/R in an_toplevel
------------------------- :: Axiom
G |- F ~>h a


G |- a ~>h a'
---------------------------- :: ConvTerm
 G |- a |> g ~>h a' |> g


Value v
------------------------------------- :: Combine
G |- (v |> g1) |> g2 ~>h v |> (g1 ; g2)

% Not used (or needed)

%   G ; dom G |- g : all rho x1:A1 -> B1 ~ all rho x2:A2 -> B2
%   a1 = a { x2 |> sym (piFst g) / x1 }
%   g2 = g @ refl2 (# x2 |> sym (piFst g) #) x2 (# piFst g #)
% ------------------------------------------------------------------------- :: Push
%   G |- ( (\rho x1:A1.a) |> g ) rho b ~>h (\rho x2:A2. (a1 |> g2)) rho b

%   G ; dom G |- g : all c1:phi1. A1 ~ all c2:phi2. A2
%   a1 = a { c2 |> sym (cpiFst g)  / c1 }
%   g2 = g @ (c2 |> sym (cpiFst g), c2)
% ------------------------------------------------------------------ :: CPush
%   G |- ( (/\c1:phi1.a) |> g ) [g1] ~>h (/\c2:phi2. (a1 |> g2)) [g1]



Value v
G ; dom G |- g : all rho x1: A1/R1 -> B1 ~ all rho x2: A2/R2 -> B2 : R
b' = b |> sym (piFst g)
g' = g @ refl2 b' b (piFst g)
---------------------------------------------------------- :: Push
G |- (v |> g) rho b ~>h (v rho b') |> g'


Value v
G ; dom G |- g : all c1:phi1. A1 ~ all c2:phi2. A2 : R
g1' = g1 |> sym (cpiFst g)
g' = g @ ( g1' , g1 )
-------------------------------------------------------- :: CPush
G |- (v |> g) [g1] ~>h (v [g1']) |> g'
